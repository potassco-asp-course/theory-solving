% ----------------------------------------------------------------------
\begin{frame}[c]{send+more=money}
  \begin{columns}
    \begin{column}{.35\textwidth}
      \begin{tabular}[t]{ccccc}
         &s&e&n&d\\
        +&m&o&r&e\\
        \hline
        m&o&n&e&y
      \end{tabular}
    \end{column}
    \begin{column}{.35\textwidth}
      Each letter corresponds exactly to one digit and all variables have to be
      pairwisely distinct
    \end{column}
  \end{columns}
\pause
\bigskip\bigskip
  \begin{columns}
    \begin{column}{.35\textwidth}
      \begin{tabular}[t]{ccccc}
         &9&5&6&7\\
        +&1&0&8&5\\
        \hline
        1&0&6&5&2
      \end{tabular}
    \end{column}
    \begin{column}{.35\textwidth}
      The example has exactly one solution
    \end{column}
  \end{columns}
\[
\{\ s\mapsto9, e\mapsto5, n\mapsto6, d\mapsto7, m\mapsto1, o\mapsto0, r\mapsto8, y\mapsto2\ \}
\]
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile,shrink=1]{send+more=money}
\begin{lstlisting}
                        sum(4,m).
arg(1,3,s). arg(2,3,m). sum(3,o).
arg(1,2,e). arg(2,2,o). sum(2,n).
arg(1,1,n). arg(2,1,r). sum(1,e).
arg(1,0,d). arg(2,0,e). sum(0,y).

not_null(m).

digit(D) :- arg(_,_,D).
digit(D) :-   sum(_,D).

&dom { 0..9 } = D :- digit(D).

&sum {  M*D : arg(I,N,D), M=10**N;
       -M*D :   sum(N,D), M=10**N  } = 0.

&sum { D } > 0 :- not_null(D).

&distinct { D : digit(D) }.

&show { D : digit(D) }.
\end{lstlisting}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile,shrink=1]{send+more=money}
\begin{lstlisting}
digit(1,3,s).   digit(2,3,m).   digit(sum,4,m).
digit(1,2,e).   digit(2,2,o).   digit(sum,3,o).
digit(1,1,n).   digit(2,1,r).   digit(sum,2,n).
digit(1,0,d).   digit(2,0,e).   digit(sum,1,e).
                                digit(sum,0,y).#(\pause#)
base(10).
exp(E) :- digit(_,E,_).

power(1,0).
power(B*P,E) :- base(B), power(P,E-1), exp(E), E>0.

number(N) :- digit(N,_,_), N!= sum.
high(D) :- digit(N,E,D), not digit(N,E+1,_).#(\pause#)

&dom {0..9} = X :- digit(_,_,X).

&sum { M*D : digit(N,E,D),   power(M,E), number(N);
      -M*D : digit(sum,E,D), power(M,E)           } = 0.


&sum { D } > 0 :- high(D).

&distinct { D : digit(_,_,D) }.

&show { D : digit(_,_,D) }.
\end{lstlisting}
\end{frame}
% ----------------------------------------------------------------------
%
%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../../main"
%%% End:
